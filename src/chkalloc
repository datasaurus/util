#!/usr/bin/awk -f
#
#- chkalloc --
#-	This script reads output from the diagnostic allocators.  It reports
#-	on any leaks it finds.  See chkalloc (1).
#- 
# Copyright (c) 2008 Gordon D. Carrie
# Licensed under the Open Software License version 3.0
#
# Please send feedback to dev0@trekix.net
#
# $Revision: 1.3 $ $Date: 2008/12/17 22:55:56 $

/(0x)?[0-9]+.* allocated / {
    cnt[$1]++
    tot++
}
/(0x)?[0-9]+.* freed / {
    cnt[$1]--
    tot++
}
END {
    if (ENVIRON["CHKALLOC_WARN"] && tot == 0) {
	printf "Warning chkalloc did not receive input.\n" >"/dev/stderr"
	exit 2
    }
    leak=0
    for (a in cnt) {
	if (cnt[a] > 0) {
	    printf "Leak at %s\n", a
	    leak++
	}
    }
    exit (leak == 0) ? 0 : 1
}
